
//Name: Simple Guessing Game
//Author: Taylor Howell
//This is a sample program that creates and manages guessing game.

//The states are created and connected at runtime.

//The program will ask the user for their name, then ask them to guess a number between 1 and 10.
//If the user guesses correctly, the program will ask if they want to play again.
//If the user guesses incorrectly, the program will ask them to guess again.
//The program will exit if the user enters "exit" or "quit" when asked if they want to play again.
//The program will also exit if the user enters "exit" or "quit" when asked to guess a number.

//1. create your variables here
create

    nameVariable text = ""
    guessVariable integer = 2

    max integer = 10
    min integer = 0

end create

//2. import the functions you need and assign the variables
import

    using GreetUser
        name = nameVariable

    using Guess
        name = nameVariable
        guess = guessVariable

    using SetRandomNumber
        max = max
        min = min
        valueToChange = guessVariable

    using ExitQuestion


end import

//3. define each of your states here
define
    
    //define the start and fallback states
    start s : GreetUser
    fallback exit

    //define intermediary states
    state guessNumber : Guess
    state resetNumber : SetRandomNumber
    state exitQuestion : ExitQuestion

end define

//4. connect the states together with transitions
connect

    startGuessing = s -> guessNumber : 1
    guessFail = guessNumber -> guessNumber : 0
    guessSuccess = guessNumber -> exitQuestion : 1
   
    exitQuestionFail = exitQuestion -> resetNumber : 0
    resetNumberSuccess = resetNumber -> guessNumber : 1

    fail = s -> s : 0
    exit = s -> exit : -1
    exitQuestionSuccess = exitQuestion -> exit : 1

end connect